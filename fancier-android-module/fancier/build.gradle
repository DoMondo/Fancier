plugins {
    id 'com.android.library'
}

apply plugin: 'com.ydq.android.gradle.native-aar.export'
apply plugin: 'maven-publish'

group = 'com.github.DoMondo'

android {
    compileSdk 31

    defaultConfig {
        minSdk 19
        targetSdk 31
        consumerProguardFiles "consumer-rules.pro"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets.main {
        java.srcDirs = [
            '../../java/project/android/src/main/java',
            '../../java/project/core/src/main/java'
        ]
    }
    externalNativeBuild {
        cmake {
            path file('src/CMakeLists.txt')
        }
    }

    nativeBundleExport {
        headerDir = "build/includes"
        excludeHeaderFilter("**/internal/**")
    }

}

task copyIncludesCore(type: Copy) {
    description = 'Copies includes'
    from "${project.projectDir}/../../native/core/include"
    into 'build/includes'
    include('**/*.h')
}
task copyIncludesPlugin(type: Copy) {
    description = 'Copies includes'
    from "${project.projectDir}/../../native/plugin/tiling/include"
    into 'build/includes'
    include('**/*.h')
}

project.afterEvaluate{
    project.getTasks().getByName("javaPreCompileRelease").dependsOn(copyIncludesCore)
    project.getTasks().getByName("javaPreCompileRelease").dependsOn(copyIncludesPlugin)
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.DoMondo'
            version = version
            artifactId = project.name
            afterEvaluate {
                from components.release
            }
        }
    }
}